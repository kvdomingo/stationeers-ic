pin DaylightSensor d0;
pin OccupancySensor d1;
pin Larre d2;

// Grow light settings
const MINUTES_IN_LIGHT = 12.5;
const DEGREES_PER_MINUTE = 18;

// Hydroponics Larre stations
const NUM_PLANTERS = 10;
const PLANTER_STATION_ROW_1_START = 8;
const PLANTER_STATION_ROW_1_END = 12;
const PLANTER_STATION_ROW_2_START = 1;
const PLANTER_STATION_ROW_2_END = 5;
const HARVEST_DROP_STATION = 6;
const SEED_PICKUP_STATION = 7;

// Status
const STATUS_EMPTY = 0;
const STATUS_WAIT_FOR_SEED = 1;
const STATUS_WAIT_FOR_HARVEST = 2;

void Main() {
    var currentStatus = STATUS_EMPTY;
    var countForNextStatus = NUM_PLANTERS;

    while (true) {
        HandleLights();
        countForNextStatus = GatherStatusLoop(currentStatus, countForNextStatus);

        currentStatus = (currentStatus + 1) % 3;
        countForNextStatus = NUM_PLANTERS;
    }
}

real GatherStatusLoop(status, count) {
    while (count > 0) {
        for (
            var station = PLANTER_STATION_ROW_1_START;
            station <= PLANTER_STATION_ROW_1_END;
            station = station + 1
        ) {
            count = GatherStatusInnerLoop(station, status, count);
        }
        for (
            var station = PLANTER_STATION_ROW_2_START;
            station <= PLANTER_STATION_ROW_2_END;
            station = station + 1
        ) {
            count = GatherStatusInnerLoop(station, status, count);
        }
    }
    return count;
}

real GatherStatusInnerLoop(station, status, count) {
    Larre.Setting = station;
    WaitUntilLarreIdle();

    if (count > 0) {
        if (status == STATUS_EMPTY & !Larre.Slots[255].Occupied) {
            count = count - 1;
        }
        if (status == STATUS_WAIT_FOR_SEED & Larre.Slots[255].Seeding) {
            count = count - 1;
        }
        if (status == STATUS_WAIT_FOR_HARVEST & Larre.Slots[255].Mature) {
            count = count - 1;
        }
    }

    return count;
}

void WaitUntilLarreIdle() {
    while (!Larre.Idle) {
        yield;
        HandleLights();
    }
}

void HandleLights() {
    var degreesPerDay = DEGREES_PER_MINUTE * MINUTES_IN_LIGHT;
    var halfLightBudget = degreesPerDay / 2;

    DevicesOfType("StructureGrowLight").On = DaylightSensor.SolarAngle < halfLightBudget;
    DevicesOfType("StructureLightLongAngled").On = OccupancySensor.Activate;
    DevicesOfType("StructureLightLongWide").On = OccupancySensor.Activate;
}
