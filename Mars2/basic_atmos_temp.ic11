pin GasSensor d0;
pin TempDisplay d1;
pin PctO2Display d2;
pin PctN2Display d3;
pin PctCO2Display d4;
pin PressureDisplay d5;

const ABS_ZERO = 273;
const MIN_TEMP_C = 18;
const TARGET_TEMP_C = 21;
const MAX_TEMP_C = 24;

const DISPLAY_MODE_PERCENT = 1;
const DISPLAY_MODE_TEMPERATURE = 4;
const DISPLAY_MODE_PRESSURE = 14;

void Main() {
    var MIN_TEMP = ABS_ZERO + MIN_TEMP_C;
    var TARGET_TEMP = ABS_ZERO + TARGET_TEMP_C;
    var MAX_TEMP = ABS_ZERO + MAX_TEMP_C;

    TempDisplay.Mode = DISPLAY_MODE_TEMPERATURE;
    PressureDisplay.Mode = DISPLAY_MODE_PRESSURE;
    PctN2Display.Mode = DISPLAY_MODE_PERCENT;
    PctCO2Display.Mode = DISPLAY_MODE_PERCENT;
    PctO2Display.Mode = DISPLAY_MODE_PERCENT;

    while (true) {
        yield;
        
        if (GasSensor.Temperature < MIN_TEMP) {
            DevicesOfType("StructureWallHeater").On = 1;
        }
        if (GasSensor.Temperature <= TARGET_TEMP) {
            DevicesOfType("StructureWallCooler").On = 0;
        }
        if (GasSensor.Temperature >= TARGET_TEMP) {
            DevicesOfType("StructureWallHeater").On = 0;
        }
        if (GasSensor.Temperature > MAX_TEMP) {
            DevicesOfType("StructureWallCooler").On = 1;
        }

        TempDisplay.Setting = GasSensor.Temperature - ABS_ZERO;
        PressureDisplay.Setting = GasSensor.Pressure * 1000;
        PctN2Display.Setting = GasSensor.RatioNitrogen;
        PctCO2Display.Setting = GasSensor.RatioCarbonDioxide;
        PctO2Display.Setting = GasSensor.RatioOxygen;
    }
}
